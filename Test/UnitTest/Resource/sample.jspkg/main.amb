top: VBox {
    message: HBox {
	    logo: Image {
			name: String "logo"
	    }
	    text_a: TextField {
			text: String "This is text field"
	    }
		graphics: Graphics2D {
			origin_x: Float -1.0
			origin_y: Float -1.0
			size_x:   Float  2.0
			size_y:   Float  2.0
			draw: Event(context, count) %{
				//console.log("[Grahics2D] " + count) ;
				let lframe = context.logicalFrame ;
				let offset = Math.PI * ((count % 10) / 10.0) ; 
				context.moveTo(lframe.x, Math.sin(lframe.x + offset)) ;
				for(let x=lframe.x ; x<=lframe.x + lframe.width ; x+=0.1) {
					let y = Math.sin(x + offset) ;
					context.lineTo(x, y) ;
				}
			%}
			init: Init %{
				console.log("[Graphics2D] Init") ;
				self.start(1.0, 10.0) ;
			%}
		}

		bitmap: Bitmap {
			draw: Event(context, count) %{
				let pos  = count % 10 ;
				context.clean() ;
				context.set(pos, pos, Color.blue) ;
			%}
			init: Init %{
				console.log("[Bitmap] Init") ;
				self.start(1.0, 10.0) ;
			%}
		}

    }
    boxes: HBox {
		checkBox0: CheckBox {
			title:	String "option-0"
			pressed: Event(status) %{
				console.log("option-0: " + status) ;
			%}
		}
		checkBox1: CheckBox {
			title:	String "option-1"
			status: Bool false
			pressed: Event(status) %{
				console.log("option-1: " + status) ;
			%}
		}
		button0: Button {
			title:	String Listner(state: top.message.graphics.state) %{
				let result = "Unknown" ;
				switch(state) {
					case AnimationState.idle:
						result = "Start" ;
					break ;
					case AnimationState.run:
						result = "Stop" ;
					break ;
					case AnimationState.pause:
						result = "Stop" ;
					break ;
				}
				return result ;
			%}

			pressed: Event() %{
				let grp = top.message.graphics ;
				let bmp = top.message.bitmap ;
				switch(grp.state){
				  case AnimationState.idle:
					top.message.graphics.start(1.0, 10.0) ;
					top.message.bitmap.start(1.0, 10.0) ;
				  break ;
				  case AnimationState.run:
					top.message.graphics.stop() ;
					top.message.bitmap.stop() ;
				  break ;
				  case AnimationState.pause:
				  break ;
				}

			%}
		}
		menu0: PopupMenu {
			items: String ["a", "b", "c"]
		}
    }
	dataTable: DataTable {
		columnCount:	Int	3
		rowCount:	Int	2
	}
    table: ComponentTable {
		columnCount:	Int	3
		rowCount:		Int	2
		cell: Icon {
			image: String "calendar"
			title: String "calendar"
		}
	make: Event(col, row) %{
		let msg   = "make event: " + col + "," + row ;
		let idx   = row * 2 + col ;
		let image = "?" ;
		switch(idx){
			case 0: image = "bag" ;			break ;
			case 1: image = "book" ; 		break ;
			case 2: image = "calendar" ;	break ;
			case 3: image = "clock" ;		break ;
			case 4: image = "telescope" ;	break ;
			case 4: image = "umblrella" ;	break ;
		}
		console.log(msg) ;
		self.title = msg ;
		self.image = image ;
	%}
    pressed: Event(col, row) %{
		console.log("Component table Pressed col=" + col + ", row=" + row) ;
	%}
    }
    button_a: Button {
        title:  String "Go to Second"
        pressed: Event() %{
			console.log("launch button view .. start") ;
			enterView("button_view", function(val){
				console.log("button_view is closed: " + val) ;
			}) ;
        %}
    }
	init: Init %{
		top.boxes.menu0.items = ["Hello", "Good morning", "Good evening"]
	%}
}

